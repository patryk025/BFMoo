OBJECT=TAPE
TAPE:TYPE=ARRAY

OBJECT=TAPE_PTR
TAPE_PTR:TYPE=INTEGER
TAPE_PTR:VALUE=0

OBJECT=PROGRAM_LENGTH
PROGRAM_LENGTH:TYPE=INTEGER
PROGRAM_LENGTH:VALUE=0

OBJECT=PROGRAM_PTR
PROGRAM_PTR:TYPE=INTEGER
PROGRAM_PTR:VALUE=0
PROGRAM_PTR:ONBRUTALCHANGED={@IF("PROGRAM_PTR","_","PROGRAM_LENGTH","{RUNNING^SET(0);}","");}

OBJECT=YIELDED
YIELDED:TYPE=INTEGER
YIELDED:VALUE=0

OBJECT=STEP_BUDGET
STEP_BUDGET:TYPE=INTEGER
STEP_BUDGET:VALUE=5000

OBJECT=RUNNING
RUNNING:TYPE=INTEGER
RUNNING:VALUE=0

OBJECT=CURRENT_INSTR
CURRENT_INSTR:TYPE=STRING
CURRENT_INSTR:VALUE=

OBJECT=JUMP_TABLE
JUMP_TABLE:TYPE=ARRAY

OBJECT=BF_CODE
BF_CODE:TYPE=STRING
BF_CODE:ONBRUTALCHANGED={PROGRAM_LENGTH^SET(BF_CODE^LENGTH());}

OBJECT=TMP_VALUE
TMP_VALUE:TYPE=INTEGER
TMP_VALUE:VALUE=0

OBJECT=DEC_TO_HEX
DEC_TO_HEX:TYPE=BEHAVIOUR
DEC_TO_HEX:CODE={@STRING("ANS","");@INT("N",$1);@IF("N","_","0","{ANS^SET("0");}","");@WHILE("N","!_","0","{@INT("REM",[N%16]);@IF("REM","<","10","{ANS^SET([""+REM+ANS^GET()]);}","");@IF("REM","_","10","{ANS^SET(["A"+ANS^GET()]);}","");@IF("REM","_","11","{ANS^SET(["B"+ANS^GET()]);}","");@IF("REM","_","12","{ANS^SET(["C"+ANS^GET()]);}","");@IF("REM","_","13","{ANS^SET(["D"+ANS^GET()]);}","");@IF("REM","_","14","{ANS^SET(["E"+ANS^GET()]);}","");@IF("REM","_","15","{ANS^SET(["F"+ANS^GET()]);}","");N^DIV(16);}");@IF("ANS^LENGTH()","_","1","{ANS^SET(["0"+ANS]);}","");@RETURN(ANS);}

OBJECT=LOGS
LOGS:TYPE=STRING
LOGS:VALUE=
LOGS:DEFAULT=
LOGS:TOINI=TRUE

OBJECT=DBG_TAPE
DBG_TAPE:TYPE=BEHAVIOUR
DBG_TAPE:CODE={@INT("W",8);@INT("LEFT",0);@INT("RIGHT",30000);@STRING("CHUNK","");@IF("[TAPE_PTR-W]",">","LEFT","{LEFT^SET([TAPE_PTR-W]);}","");@IF("[TAPE_PTR+W+1]","<","RIGHT","{RIGHT^SET([TAPE_PTR+W+1]);}","");@LOOP("{CHUNK^ADD([DEC_TO_HEX^RUN(TAPE^GET(_I_))+"_"]);}",LEFT,[RIGHT-LEFT],1);@RETURN(["_tape_"+LEFT+"_"+RIGHT+"_"+CHUNK]);}

OBJECT=LOG_PRE
LOG_PRE:TYPE=BEHAVIOUR
LOG_PRE:CODE={TMP_VALUE^SET(TAPE^GET(TAPE_PTR));LOGS^ADD(["PRE__ip_"+PROGRAM_PTR+"_dp_"+TAPE_PTR+"_op_"+CURRENT_INSTR+"_arg_NAN_cell_"+TMP_VALUE+"(0x"+DEC_TO_HEX^RUN(TMP_VALUE)+")"+DBG_TAPE^RUN()+"|"]);}

OBJECT=LOG_POST
LOG_POST:TYPE=BEHAVIOUR
LOG_POST:CODE={TMP_VALUE^SET(TAPE^GET(TAPE_PTR));LOGS^ADD(["POST__ip_"+PROGRAM_PTR+"_dp_"+TAPE_PTR+"_op_"+CURRENT_INSTR+"_arg_NAN_cell_"+TMP_VALUE+"(0x"+DEC_TO_HEX^RUN(TMP_VALUE)+")|"]);}

OBJECT=RENDER_VIDEO_BUFFER
RENDER_VIDEO_BUFFER:TYPE=BEHAVIOUR
RENDER_VIDEO_BUFFER:CODE={@LOOP("{@INT("PIXEL_VALUE",[TAPE^GET(_I_)]);BF_SCREEN^UPDATE_PIXEL(_I_,PIXEL_VALUE);}",0,255,1);}

OBJECT=INITIALIZE_JUMP_TABLE
INITIALIZE_JUMP_TABLE:TYPE=BEHAVIOUR
INITIALIZE_JUMP_TABLE:CODE={@LOOP("{JUMP_TABLE^ADD(-1);}",0,PROGRAM_LENGTH,1);}

OBJECT=INITIALIZE_TAPE
INITIALIZE_TAPE:TYPE=BEHAVIOUR
INITIALIZE_TAPE:CODE={@LOOP("{TAPE^ADD(0);}",0,30000,1);}

#
# BRAINFUCK SYMBOLS
#

OBJECT=INCREMENT_POINTER_CHAR
INCREMENT_POINTER_CHAR:TYPE=STRING
INCREMENT_POINTER_CHAR:VALUE=>

OBJECT=DECREMENT_POINTER_CHAR
DECREMENT_POINTER_CHAR:TYPE=STRING
DECREMENT_POINTER_CHAR:VALUE=<

OBJECT=INCREMENT_VAL_AT_POINTER_CHAR
INCREMENT_VAL_AT_POINTER_CHAR:TYPE=STRING
INCREMENT_VAL_AT_POINTER_CHAR:VALUE=+

OBJECT=DECREMENT_VAL_AT_POINTER_CHAR
DECREMENT_VAL_AT_POINTER_CHAR:TYPE=STRING
DECREMENT_VAL_AT_POINTER_CHAR:VALUE=-

OBJECT=PRINT_CHARACTER_CHAR
PRINT_CHARACTER_CHAR:TYPE=STRING
PRINT_CHARACTER_CHAR:VALUE=.

OBJECT=GET_CHARACTER_CHAR
GET_CHARACTER_CHAR:TYPE=STRING
GET_CHARACTER_CHAR:VALUE=,

OBJECT=OPEN_LOOP_CHAR
OPEN_LOOP_CHAR:TYPE=STRING
OPEN_LOOP_CHAR:VALUE=[

OBJECT=CLOSE_LOOP_CHAR
CLOSE_LOOP_CHAR:TYPE=STRING
CLOSE_LOOP_CHAR:VALUE=]

OBJECT=INCREMENT_TAPE_PTR
INCREMENT_TAPE_PTR:TYPE=BEHAVIOUR
INCREMENT_TAPE_PTR:CODE={TAPE_PTR^INC();@IF("TAPE_PTR",">_","30000","{TAPE_PTR^SET(0);}","");}

OBJECT=DECREMENT_TAPE_PTR
DECREMENT_TAPE_PTR:TYPE=BEHAVIOUR
DECREMENT_TAPE_PTR:CODE={TAPE_PTR^DEC();@IF("TAPE_PTR","<","0","{TAPE_PTR^SET(29999);}","");}

OBJECT=INCREMENT_TAPE_PTR_VAL
INCREMENT_TAPE_PTR_VAL:TYPE=BEHAVIOUR
INCREMENT_TAPE_PTR_VAL:CODE={TAPE^ADDAT(TAPE_PTR,1);@IF("TAPE^GET(TAPE_PTR)",">","255","{TAPE^CHANGEAT(TAPE_PTR,0);}","");}

OBJECT=DECREMENT_TAPE_PTR_VAL
DECREMENT_TAPE_PTR_VAL:TYPE=BEHAVIOUR
DECREMENT_TAPE_PTR_VAL:CODE={TAPE^SUBAT(TAPE_PTR,1);@IF("TAPE^GET(TAPE_PTR)","<","0","{TAPE^CHANGEAT(TAPE_PTR,255);}","");}

OBJECT=ESCAPE_FROM_LOOP
ESCAPE_FROM_LOOP:TYPE=CONDITION
ESCAPE_FROM_LOOP:OPERAND1=TAPE^GET(TAPE_PTR)
ESCAPE_FROM_LOOP:OPERATOR=EQUAL
ESCAPE_FROM_LOOP:OPERAND2=0

OBJECT=BACK_TO_START_OF_LOOP
BACK_TO_START_OF_LOOP:TYPE=CONDITION
BACK_TO_START_OF_LOOP:OPERAND1=TAPE^GET(TAPE_PTR)
BACK_TO_START_OF_LOOP:OPERATOR=NOTEQUAL
BACK_TO_START_OF_LOOP:OPERAND2=0

OBJECT=JUMP_TO_END_OF_LOOP
JUMP_TO_END_OF_LOOP:TYPE=BEHAVIOUR
JUMP_TO_END_OF_LOOP:CONDITION=ESCAPE_FROM_LOOP
JUMP_TO_END_OF_LOOP:CODE={PROGRAM_PTR^SET([JUMP_TABLE^GET(PROGRAM_PTR)]);}

OBJECT=JUMP_TO_START_OF_LOOP
JUMP_TO_START_OF_LOOP:TYPE=BEHAVIOUR
JUMP_TO_START_OF_LOOP:CONDITION=BACK_TO_START_OF_LOOP
JUMP_TO_START_OF_LOOP:CODE={PROGRAM_PTR^SET([JUMP_TABLE^GET(PROGRAM_PTR)]);}

OBJECT=READ_INPUT
READ_INPUT:TYPE=BEHAVIOUR
READ_INPUT:CODE={!HANDLE_INPUT^RUN();}

OBJECT=PREPROCESS_CODE
PREPROCESS_CODE:TYPE=BEHAVIOUR
PREPROCESS_CODE:CODE={@LOOP("{@STRING("CURRENT_CHAR",[BF_CODE^GET(_I_)]);@IF("CURRENT_CHAR","_","OPEN_LOOP_CHAR","{STACK^PUSH(_I_);}","");@IF("CURRENT_CHAR","_","CLOSE_LOOP_CHAR","{@INT("OPEN_ADDR",STACK^POP());JUMP_TABLE^CHANGEAT(OPEN_ADDR,_I_);JUMP_TABLE^CHANGEAT(_I_,OPEN_ADDR);}","");}",0,PROGRAM_LENGTH,1);}

OBJECT=EXECUTE_BF_STEP
EXECUTE_BF_STEP:TYPE=BEHAVIOUR
EXECUTE_BF_STEP:CODE={@INT("STEPS",0);YIELDED^SET(0);@WHILE("PROGRAM_PTR","<","PROGRAM_LENGTH","{@IF("YIELDED","_","0","{CURRENT_INSTR^SET([BF_CODE^GET(PROGRAM_PTR)]);LOG_PRE^RUN();@IF("CURRENT_INSTR","_","INCREMENT_POINTER_CHAR","{INCREMENT_TAPE_PTR^RUN();}","");@IF("CURRENT_INSTR","_","DECREMENT_POINTER_CHAR","{DECREMENT_TAPE_PTR^RUN();}","");@IF("CURRENT_INSTR","_","INCREMENT_VAL_AT_POINTER_CHAR","{INCREMENT_TAPE_PTR_VAL^RUN();}","");@IF("CURRENT_INSTR","_","DECREMENT_VAL_AT_POINTER_CHAR","{DECREMENT_TAPE_PTR_VAL^RUN();}","");@IF("CURRENT_INSTR","_","OPEN_LOOP_CHAR","{JUMP_TO_END_OF_LOOP^RUNC();}","");@IF("CURRENT_INSTR","_","CLOSE_LOOP_CHAR","{JUMP_TO_START_OF_LOOP^RUNC();}","");@IF("CURRENT_INSTR","_","GET_CHARACTER_CHAR","{READ_INPUT^RUN();}","");@IF("CURRENT_INSTR","_","PRINT_CHARACTER_CHAR","{YIELDED^SET(1);}","");LOG_POST^RUN();PROGRAM_PTR^INC();STEPS^INC();@IF("STEPS",">_","STEP_BUDGET","{YIELDED^SET(1);}","");}","{}");@IF("YIELDED","_","1","{ONEBREAK();}","");}");}

OBJECT=EXECUTION_LOOP
EXECUTION_LOOP:TYPE=BEHAVIOUR
EXECUTION_LOOP:CODE={@WHILE("RUNNING","_","1","{RENDER_VIDEO_BUFFER^RUN();EXECUTE_BF_STEP^RUN();}");}

OBJECT=CONSTRUCTOR
CONSTRUCTOR:TYPE=BEHAVIOUR
CONSTRUCTOR:CODE={LOGS^RESETINI();INITIALIZE_TAPE^RUN();CLSSCREENOBJ^NEW("BF_SCREEN");!BF_SCREEN^DEBUG_COLOR_PALETTE();BF_CODE^SET($2);INITIALIZE_JUMP_TABLE^RUN();CLSSTACKOBJ^NEW("STACK");PREPROCESS_CODE^RUN();!JUMP_TABLE^SAVE("TEST_ARR.ARR");RUNNING^SET(1);EXECUTION_LOOP^RUN();}
